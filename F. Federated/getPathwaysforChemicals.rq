prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix cheminf: <http://semanticscience.org/resource/CHEMINF_> 
prefix wdt: <http://www.wikidata.org/prop/direct/>
prefix wp: <http://vocabularies.wikipathways.org/wp#>
prefix aopo: <http://aopkb.org/aop_ontology#>

select distinct ?ChemicalName ?LinkedAOP ?LinkedAOPURI ?CASRN ?ChemicalURI ?PathwayID ?PathwayURI where{
select *where { 
 ?cheLook a cheminf:000000 ;
 dc:identifier ?ChemicalURI ;
 dc:title ?ChemicalName ;
 cheminf:000446 ?CASRN ;
 dcterms:isPartOf ?LinkedStressor.
 ?LinkedStressor dcterms:isPartOf ?LinkedAOPURI .
 ?LinkedAOPURI a aopo:AdverseOutcomePathway ;
 rdfs:label ?LinkedAOP.
 service <https://query.wikidata.org/bigdata/namespace/wdq/sparql>{
   ?wikidata wdt:P231 ?CASRN.
   service <http://sparql.wikipathways.org/>{
     ?metabolite wp:bdbWikidata ?wikidata;
     dcterms:isPartOf ?PathwayURI.
     ?PathwayURI a wp:Pathway ;
     dcterms:identifier ?PathwayID.
}}}} order by ?ChemicalName
