#This query takes all Stressor chemicals in the AOP-Wiki and their mappings to ChEBI IDs 
#Then, these ChEBI IDs are used to look up human molecular pathways in WikiPathways

PREFIX wp: <http://vocabularies.wikipathways.org/wp#>

SELECT DISTINCT ?ChemicalName ?mappedid
?LinkedStressor (group_concat(?LinkedAOP;separator=',') as ?AOPs) (STR(?PathwayID) AS ?Pathway_ID) 
(STR(?PathwayTitle) AS ?PathwayName) ?PathwayURI 

WHERE{
   ?cheLook a cheminf:000000; dc:title ?ChemicalName ; dcterms:isPartOf ?LinkedStressor ;
     skos:exactMatch ?mappedid .
   ?LinkedStressor dcterms:isPartOf ?LinkedAOPURI .
   ?LinkedAOPURI a aopo:AdverseOutcomePathway ; rdfs:label ?LinkedAOP.
   ?mappedid a cheminf:000407; cheminf:000407 ?ChEBI.
   # Adjust the format of the ChEBI ID to match WikiPathways' structure
   BIND(IRI(CONCAT("https://identifiers.org/chebi/CHEBI:", STRAFTER(STR(?mappedid), "https://identifiers.org/chebi/"))) AS ?ChEBI_URI)
   SERVICE <https://sparql.wikipathways.org/sparql/>{ 
     ?metabolite wp:bdbChEBI ?ChEBI_URI ; dcterms:isPartOf ?PathwayURI.
     ?PathwayURI a wp:Pathway ; dcterms:identifier ?PathwayID ; 
                   dc:title ?PathwayTitle ; wp:organismName "Homo sapiens" .}}
ORDER BY DESC (?ChemicalName)
